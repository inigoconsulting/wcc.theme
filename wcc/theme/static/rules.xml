<?xml version="1.0" encoding="UTF-8"?>
<!--start main rule, only theme if #visual-portal-wrapper exist -->
<rules
    xmlns="http://namespaces.plone.org/diazo"
    xmlns:css="http://namespaces.plone.org/diazo/css"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:xi="http://www.w3.org/2001/XInclude"
    css:if-content="#visual-portal-wrapper">
    
    <!--HEAD SECTION-->
    <!-- Drop styles in the head - these are added back by including them from Plone -->
    <drop theme="/html/head/style | /html/head/script | /html/head/link | /html/head/title" />
    <prepend theme="/html/head" content="/html/head/base | /html/head/link | /html/head/style | /html/head/script | /html/head/meta | /html/head/title" />
    <prepend css:theme=".main-wrapper" css:content="#portal-personaltools"/>

    <!--logo-->
    <replace css:theme="#logo" css:content="#portal-logo > a"/>
    <replace css:theme-children=".tagline" css:content-children="#portal-logo .logo-description"/>
    <drop css:theme=".logo-container h1.title" attributes="class"/>
    
    <!--BODY-->
    <merge theme="/html/body" content="/html/body" attributes="class"/>
    <merge theme="/html/body" content="/html/body" attributes="dir"/>
    
    <!--navigation-->
    <replace css:theme-children='#portal-globalnav' 
              css:content-children='#portal-globalnav' />
    <replace 
        css:theme-children='#portal-languageselector' 
        css:content-children='#portal-languageselector'/>
    <replace
        css:theme-children=".footer-link"
        css:content-children="#portal-siteactions"/>

    <!--homepage-->

    <rules css:if-content="#homepage-view.homepage-full-width">
        <theme href="index-subsite.html"/>
        <after css:theme=".focus-container">
            <div style="margin-top:20px">
                <xsl:copy-of css:select="#edit-bar"/>
            </div>
        </after>
        <replace css:theme-children=".three-col-items">
            <xsl:for-each css:select="#homepage-news .homepage-newsitem">
                <div>
                    <xsl:if test="div/div/img">
                        <h2><xsl:copy-of select="div/div/h3/a"/></h2>
                        <xsl:copy-of select="div/div/img"/>
                        <p><xsl:value-of select="div/div/p/text()"/></p>
                    </xsl:if>
                    <xsl:if test="not(div/div/img)">
                        <h2><xsl:copy-of select="div/div/h3/a"/></h2>
                        <p><xsl:value-of select="div/div/p/text()"/></p>
                    </xsl:if>
                </div>
            </xsl:for-each>
        </replace>
        <replace css:theme=".focus-container" css:content="#homepage-slider"/>
    </rules>

    <rules css:if-content="#homepage-view.homepage-normal-width">
        <theme href="index.html" />
        <after css:theme=".focus-container">
            <div style="margin-top:20px">
                <xsl:copy-of css:select="#edit-bar"/>
            </div>
        </after>
        <replace css:theme=".focus-container" css:content="#homepage-slider"/>
        <drop css:theme=".main-page.right-content" css:if-not-content="#homepage-news-events-listing"/>
        <replace css:theme-children=".news-list-container">
            <xsl:for-each css:select="#homepage-news .homepage-newsitem">
                <div class="clearfix news-list-item">
                    <xsl:if test="div/div/img">
                        <div class="news-img"><xsl:copy-of select="div/div/img"/></div>
                        <div class="news-content">
                            <h2><xsl:copy-of select="div/div/h3/a"/></h2>
                            <p><xsl:value-of select="div/div/p/text()"/></p>
                        </div>
                    </xsl:if>
                    <xsl:if test="not(div/div/img)">
                        <h2><xsl:copy-of select="div/div/h3/a"/></h2>
                        <p><xsl:value-of select="div/div/p/text()"/></p>
                    </xsl:if>
                </div>

            </xsl:for-each>
            <div class="clearfix news-list-morelink">
                <xsl:copy-of css:select=".homepage-morenews > a"/>
            </div>
        </replace>

        <replace css:theme-children=".events-list-container">
            <xsl:if test="//h2[@id='homepage-events-title']/text()">
                <h2 class="portlet-header"><xsl:value-of select="//h2[@id='homepage-events-title']/text()"/></h2>
            </xsl:if>
            <xsl:for-each css:select=".homepage-event">
                <div class="clearfix events-list-item">
                    <xsl:copy-of select="p"/>
                </div>
            </xsl:for-each>
        </replace>

    </rules>

    <rules if-content="//body[contains(@class,'template-edit')] or //body[contains(@class,'template-atct_edit')]">
        <theme href="content.html" />
    </rules>
    <!--content left-->
    <rules if-content="//div[@id='portal-column-one'] and //div[@id='portal-column-two']">
        <theme href="center-content.html" />
    </rules>
    <rules if-content="//div[@id='portal-column-one']">
        <theme href="right-content.html" />
    </rules>
    <!--content right-->
    <rules if-content="//div[@id='portal-column-two']">
        <theme href="left-content.html" />
    </rules>
    <!--content/nocolumn-->
    <rules if-not-content="//div[@id='portal-column-one'] and //div[@id='portal-column-two']">
        <theme href="content.html" />
    </rules>
    
    <replace theme-children="//article[contains(@class, 'main-page')]" content-children="//div[@id='portal-column-content']"
        css:if-not-content="#homepage-view"/>
    <replace css:theme-children="aside.left" css:if-content="#portal-column-one">
        <xsl:for-each css:select="#portal-column-one .portletWrapper">
            <xi:include href="portlet-rules.xml"/>
        </xsl:for-each>
        <xsl:copy-of css:select="#portletmanager-plone-leftcolumn"/>
        <xsl:copy-of css:select="#portal-column-one .managePortletsLink"/>
    </replace>
    <drop css:theme-children="aside.left" css:if-not-content="#portal-column-one"/> 
    <replace css:theme-children="aside.right" css:if-content="#portal-column-two">
        <xsl:for-each css:select="#portal-column-two .portletWrapper">
            <xi:include href="portlet-rules.xml"/>
        </xsl:for-each>
        <xsl:copy-of css:select="#portletmanager-plone-rightcolumn"/>
        <xsl:copy-of css:select="#portal-column-two .managePortletsLink"/>
    </replace>
    <drop css:theme-children="aside.right" css:if-not-content="#portal-column-two"/> 

    <after css:theme="#main" css:content="#plone-analytics"/>
    
</rules><!--end main rule-->
